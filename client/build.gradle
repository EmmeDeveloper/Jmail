plugins {
    id 'application'
    id 'io.freefair.lombok'
    id "com.diffplug.spotless"
    id 'org.openjfx.javafxplugin'
}

version = '30L'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
    implementation 'com.google.guava:guava:30.1.1-jre'

    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.14.0'

    implementation 'org.kordamp.ikonli:ikonli-javafx:12.3.1'
    implementation 'org.kordamp.ikonli:ikonli-fontawesome5-pack:12.3.1' // cheat-sheet here https://kordamp.org/ikonli/cheat-sheet-fontawesome5.html

    implementation('com.github.eduardz1:custom_window:50747b0328') {
        because 'customization of Windows 11 window, no effect on other OS'
    }

    implementation project(':lib')
}

application {
    mainClass = 'jmail.client.Main'
}

javafx {
    version = "19"
    modules = [
        "javafx.controls",
        "javafx.fxml",
    ]
}

tasks.named('configJavafxRun').configure {
    notCompatibleWithConfigurationCache('issue tracked https://github.com/openjfx/javafx-gradle-plugin/issues/136')
}

tasks.named('test') {
    useJUnitPlatform()
}

spotless {
    ratchetFrom 'origin/main'

    java {
        importOrder() // Use the default importOrder configuration
        removeUnusedImports()
        googleJavaFormat()
        formatAnnotations()
    }

    groovyGradle {
        target '*.gradle', 'gradle/**/*.gradle'
        greclipse()
        indentWithSpaces()
    }

    format 'css', {
        target 'src/**/*.css'
        prettier()
    }

    format 'xml', {
        target 'src/**/*.xml', 'src/**/*.fxml'
        eclipseWtp('xml')
        indentWithSpaces()
    }
}
