plugins {
    id 'application'
    id 'io.freefair.lombok'
    id 'org.openjfx.javafxplugin'
    id "com.diffplug.spotless"
}

version = '30L'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
    implementation 'com.google.guava:guava:30.1.1-jre'

    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.14.0'

    implementation('com.github.eduardz1:custom_window:181a04351f') {
        because 'customization of Windows 11 window, no effect on other OS'
    }

    implementation project(':lib')
}

application {
    mainClass = 'jmail.client.Main'
}

javafx {
    version = "17"
    modules = [
        'javafx.controls',
        'javafx.fxml'
    ]
}

tasks.named('test') {
    useJUnitPlatform()
}

spotless {
    ratchetFrom 'origin/main'

    java {
        importOrder() // Use the default importOrder configuration
        removeUnusedImports()
        googleJavaFormat()
        formatAnnotations()
    }

    groovyGradle {
        target '*.gradle', 'gradle/**/*.gradle'
        greclipse()
        indentWithSpaces()
    }

    format 'css', {
        target 'src/**/*.css'
        prettier()
    }

    format 'xml', {
        target 'src/**/*.xml', 'src/**/*.fxml'
        eclipseWtp('xml')
        indentWithSpaces()
    }
}


tasks.register('installLocalGitHook', Copy) {
    from rootProject.file('scripts/pre-commit')
    into rootProject.file('.git/hooks')
    fileMode 0775

    from rootProject.file('scripts/post-commit')
    into rootProject.file('.git/hooks')
    fileMode 0775
}

build.dependsOn = [
    'spotlessApply',
    'installLocalGitHook'
]

run {
    jvmArgs("-Dprism.forceUploadingPainter=true") {
        because 'fix for transaprency effect on border of window'
    }
}
